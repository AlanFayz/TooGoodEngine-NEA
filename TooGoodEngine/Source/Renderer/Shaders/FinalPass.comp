#version 460 core 

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform image2D u_FinalImage;

/*
   Tone mapping algorithm maps values from hdr to ldr
   http://filmicworlds.com/blog/filmic-tonemapping-operators/
*/

uniform float u_Gradient;

vec3 Uncharted2TonemapPartial(vec3 x)
{
    float A = 0.15f;
    float B = 0.50f;
    float C = 0.10f;
    float D = 0.20f;
    float E = 0.02f;
    float F = 0.30f;
    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

vec3 Uncharted2Filmic(vec3 v)
{
    vec3 curr = Uncharted2TonemapPartial(v * u_Gradient);

    vec3 W = vec3(11.2f);
    vec3 whiteScale = vec3(1.0f) / Uncharted2TonemapPartial(W);
    return curr * whiteScale;
}

void main()
{
	ivec2 coordinate = ivec2(gl_GlobalInvocationID);

    vec4 currentColor = imageLoad(u_FinalImage, coordinate);

    currentColor.rgb = Uncharted2Filmic(currentColor.rgb);

    //converts color from linear to non linear 
    currentColor = pow(currentColor, vec4(1.0/2.2));  

    imageStore(u_FinalImage, coordinate, currentColor);
}
